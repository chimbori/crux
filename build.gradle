apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.chimbori.crux'
archivesBaseName = 'crux'
version = '2.0.2'

description = """Crux parses Web pages to identify the crux of an article — the very essential 
  points — minus all the fluff. The library consists of multiple independent APIs. You can pick and 
  choose which ones you want to use. If you use Crux in an Android app, they are designed to be  
  independent so that Proguard or other minification tools can strip out the parts you don’t use."""

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
  mavenCentral()
}

dependencies {
  // https://github.com/jhy/jsoup/issues/911 prevents upgrading to JSoup 1.11.2.
  implementation 'org.jsoup:jsoup:1.9.2'
  implementation 'org.apache.commons:commons-lang3:3.7'
  testImplementation 'junit:junit:4.12'
}

configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives jar, javadocJar, sourcesJar
}

if (project.hasProperty("ossrhUsername")) {
  signing {
    sign configurations.archives
  }

  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        pom.project {
          name 'Crux'
          description 'Crux parses Web pages to identify the crux of an Article'
          url 'https://github.com/chimbori/crux'
          packaging 'jar'
          inceptionYear '2016'

          scm {
            url 'https://github.com/chimbori/crux'
            connection 'scm:git:git://github.com/chimbori/crux.git'
            developerConnection 'scm:git:ssh://github.com:chimbori/crux.git'
          }

          issueManagement {
            system 'GitHub'
            url 'https://github.com/chimbori/crux/issues'
          }

          licenses {
            license {
              name 'The Apache Software License, Version 2.0'
              url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              distribution 'repo'
            }
          }

          developers {
            developer {
              id 'chimbori'
              name 'Chimbori'
              email 'hello@chimbori.com'
            }
          }
        }
      }
    }
  }
}
