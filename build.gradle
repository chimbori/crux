apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.chimbori.crux'
archivesBaseName = 'crux'
version = '3.0.0'

description = """Crux parses Web pages to identify the crux of an article — the very essential
  points — minus all the fluff. The library consists of multiple independent APIs. You can pick and
  choose which ones you want to use. If you use Crux in an Android app, they are designed to be
  independent so that Proguard or other minification tools can strip out the parts you don’t use."""

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()
}

dependencies {
  implementation("org.jsoup:jsoup:1.13.1")
  implementation("org.apache.commons:commons-text:1.9")
  implementation("com.squareup.okhttp3:okhttp:4.9.0")
  testImplementation("junit:junit:4.12")
  implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version")
}

configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}
task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}
task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}
artifacts {
  archives jar, javadocJar, sourcesJar
}

buildscript {
  ext.kotlin_version = '1.4.21'
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}
compileKotlin {
  kotlinOptions {
    allWarningsAsErrors = true
    jvmTarget = "1.8"
  }
}
compileTestKotlin {
  kotlinOptions {
    allWarningsAsErrors = true
    jvmTarget = "1.8"
  }
}

if (project.hasProperty("ossrhUsername")) {
  signing {
    sign configurations.archives
  }

  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        pom.project {
          name 'Crux'
          description 'Crux parses Web pages to identify the crux of an Article'
          url 'https://github.com/chimbori/crux'
          packaging 'jar'
          inceptionYear '2016'

          scm {
            url 'https://github.com/chimbori/crux'
            connection 'scm:git:git://github.com/chimbori/crux.git'
            developerConnection 'scm:git:ssh://github.com:chimbori/crux.git'
          }

          issueManagement {
            system 'GitHub'
            url 'https://github.com/chimbori/crux/issues'
          }

          licenses {
            license {
              name 'The Apache Software License, Version 2.0'
              url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              distribution 'repo'
            }
          }

          developers {
            developer {
              id 'chimbori'
              name 'Chimbori'
              email 'hello@chimbori.com'
            }
          }
        }
      }
    }
  }
}
